# Top level autoconf configuration file
#
# Copyright (C) 2013 Embecosm Limited
#
# Contributor Jeremy Bennett <jeremy.bennett@embecosm.com>
#
# This file is part of the RISC-V GDB server
# 
# This program is free software; you can redistribute it and/or modify it
# under the terms of the GNU General Public License as published by the Free
# Software Foundation; either version 3 of the License, or (at your option)
# any later version.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for
# more details.
#
# You should have received a copy of the GNU General Public License along
# with this program.  If not, see <http://www.gnu.org/licenses/>.


# Process this file with autoconf to produce a configure script.

AC_INIT([riscv-gdbserver], [0.0.2], [support@embecosm.com])
AM_INIT_AUTOMAKE
LT_INIT
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIRS([m4])

AC_COPYRIGHT([Copyright (C) 2017 Embecosm Limited])

# Make sure we are using a reasonably recent autoconf version
AC_PREREQ(2.69)

# We're using C++
AC_LANG(C++)

# C++ compiler and install utility
AC_PROG_CXX
AC_PROG_INSTALL

# Add -Werror to the CXXFLAGS. For now this is not configurable.
CXXFLAGS="$CXXFLAGS -Werror -std=gnu++11"

AC_MSG_CHECKING([pkg-config m4 macros])
if test m4_ifdef([PKG_CHECK_MODULES], [yes], [no]) == yes; then
	AC_MSG_RESULT([yes]);
else
	AC_MSG_RESULT([no]);
	AC_MSG_ERROR([
pkg-config is required.
See pkg-config.freedesktop.org])
fi

# Try to locate the Verilator headers using pkg-config
PKG_CHECK_MODULES([VERILATOR], [verilator >= 3.905],
                  [VERILATOR_PC=1], [VERILATOR_PC=0])

# If we don't find the Verilator headers with pkg-config, they can be manually
# specified.
AC_ARG_WITH(
	[verilator-headers],
	AC_HELP_STRING([--with-verilator-headers],
	               [location of standard verilator headers]),
	[USERVINCDIR="$withval"],[USERVINCDIR=0])

AS_IF(["$USERVINCDIR" != 0], [VCPPFLAGS="-I$USERVINCDIR -I$USERVINCDIR/vltstd"],
      ["$VERILATOR_PC" == 1], [VCPPFLAGS="$VERILATOR_CFLAGS"],
      [VCPPFLAGS="-Iusr/share/verilator/include -Iusr/share/verilator/include/vltstd"])

CPPFLAGS="$CPPFLAGS $VCPPFLAGS"

# We need to know where the target library and headers are for each target,
# and the name of the top module (from which we derive the module name)

# PicoRV32
AC_ARG_WITH(
	[picorv32-modeldir],
	AC_HELP_STRING([--with-picorv32-modeldir],
	               [directory containing the PicoRV32 model & headers]),
	[MDIR_PICORV32="$withval"],
	[AC_MSG_FAILURE(PicoRV32 model directory not specified)])
AC_ARG_WITH(
	[picorv32-topmodule],
	AC_HELP_STRING([--with-picorv32-topmodule],
	               [Name of the top module in the PicoRV32 Verilog]),
	[MODNAME_PICORV32="$withval"],
	[AC_MSG_FAILURE(PicoRV32 top module not specified)])
AC_SUBST(MDIR_PICORV32)
AC_SUBST(MODNAME_PICORV32)

# RI5CY
AC_ARG_WITH(
	[ri5cy-modeldir],
	AC_HELP_STRING([--with-ri5cy-modeldir],
	               [directory containing the RI5CY model & headers]),
	[MDIR_RI5CY="$withval"],
	[AC_MSG_FAILURE(RI5CY model directory not specified)])
AC_ARG_WITH(
	[ri5cy-topmodule],
	AC_HELP_STRING([--with-ri5cy-topmodule],
	               [Name of the top module in the RI5CY Verilog]),
	[MODNAME_RI5CY="$withval"],
	[AC_MSG_FAILURE(RI5CY top module not specified)])
AC_SUBST(MDIR_RI5CY)
AC_SUBST(MODNAME_RI5CY)

# This is GNU compliant source and uses GNU libraries
AC_DEFINE(_GNU_SOURCE, 1, "The source code uses the GNU libraries)

# Check the headers. For now we'll assume that if we have the headers, we have
# the functions (no AC_CHECK_FUNCS).

# Headers for C++
AC_LANG_PUSH([C++])
AC_CHECK_HEADERS([cerrno cstdio cstdlib cstring fstream iomanip iostream], , \
                 [AC_MSG_FAILURE(C++ headers missing)])
AC_LANG_POP([C++])
# Headers for C
AC_CHECK_HEADERS([arpa/inet.h netdb.h netinet/in.h netinet/tcp.h poll.h \
                 inttypes.h sys/socket.h], , \
		 [AC_MSG_FAILURE(C headers missing)])
# Headers for Verilator
AC_CHECK_HEADERS([verilated.h verilated_dpi.h verilated_vcd_c.h], , \
		 [AC_MSG_FAILURE(Verilator headers missing)])

# check for uint{8,32,64}_t in stdint.h.
AC_CHECK_TYPES([uint8_t, uint32_t, uint64_t], , \
               [AC_MSG_FAILURE(fixed unsigned types not available)], \
               [#include <stdint.h>])

# Substitute any changed environment variables
AC_SUBST(CPPFLAGS)
AC_SUBST(CXXFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(VTESTBENCH)

# We want to create a Makefile
AC_CONFIG_FILES([Makefile                  \
                 server/Makefile           \
		 targets/Makefile          \
		 targets/common/Makefile   \
		 targets/picorv32/Makefile \
		 targets/ri5cy/Makefile])

# Put it all out.
AC_OUTPUT
